# Author: williamjcm <w.jcm59@gmail.com>
# Contributor: mosra <mosra@centrum.cz>
# Based on the Corrade Arch Linux PKGBUILD maintained by xyproto and the MSYS2/MinGW PKGBUILD templates

_realname=corrade
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2018.10
pkgrel=1
pkgdesc='C++11/C++14 multiplatform utility library'
provides=('corrade')
arch=('any')
url='https://magnum.graphics/corrade/'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" 'git' "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${_realname}"::"git+https://github.com/mosra/corrade.git#tag=v$pkgver")
sha1sums=('SKIP')

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      ../${_realname}

  ninja
}

package() {
  #DESTDIR="$pkgdir" ninja -C build-${CARCH} install
  cd "${srcdir}"/build-${CARCH}
  DESTDIR="${pkgdir}" ninja install
  
  install -Dm644 "${srcdir}/${_realname}/COPYING" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}

# vim: ts=2 sw=2 et:
# getver: no
