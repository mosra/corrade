# Author: mosra <mosra@centrum.cz>
pkgname=corrade-git
pkgver=snapshot.2014.06.r67.g7d38a8a
pkgrel=1
pkgdesc="C++11 multiplatform utility library (Git version)"
arch=('i686' 'x86_64')
url="http://mosra.cz/blog/corrade.php"
license=('MIT')
makedepends=('cmake' 'git')
provides=('corrade')
conflicts=('corrade')
source=("git+git://github.com/mosra/corrade.git")
sha1sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake "$srcdir/${pkgname%-git}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir/" install
}
