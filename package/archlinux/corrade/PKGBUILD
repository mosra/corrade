# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: mosra <mosra@centrum.cz>

pkgname=corrade
pkgver=2018.04
pkgrel=1
pkgdesc='C++11/C++14 multiplatform utility library'
arch=('x86_64')
url='http://magnum.graphics/corrade/'
license=('MIT')
makedepends=('cmake' 'git' 'ninja')
source=("git+https://github.com/mosra/corrade.git#tag=v$pkgver")
sha1sums=('SKIP')

build() {
  mkdir -p build
  cd build
  cmake ../corrade \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "corrade/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
# getver: no
